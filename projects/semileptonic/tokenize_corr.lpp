%option nounput
%option noyywrap
%option prefix="two_pts_corr_parser_"
%option outfile="tokenize_corr.cpp"
%option reentrant
%option bison-bridge
%option bison-locations

%{
#include <stdio.h>
#include <string.h>
#include "../../src/nissa.hpp"
#include "driver_corr.hpp"

#define CORR_NAME yyextra->corr_name[yyextra->ncorr]   
#define YY_EXTRA_TYPE two_pts_parser_driver*

#define CORR_NAME_ADD(in) if(!yyextra->curr_corr_named) CORR_NAME+=in
   
#define YY_INPUT(buf,result,max_size)		\
   {						\
     char c;					\
     int rc=fscanf(yyextra->fin,"%c",&c);	\
     if(rc!=1 || feof(yyextra->fin)) result=YY_NULL;	\
     else					\
       {					\
	 result=1;				\
	 buf[0]=c;				\
       }					\
   }
   
#include "parse_corr.hpp"
   
   const int debug_lexer=0;
%}

%%
 /* ignoring rules */
\/\/[[:alnum:][:blank:]^+-/=*()\n]* if(debug_lexer) printf("Ignoring comment: %s\n",yytext);
[[:blank:]]+ if(debug_lexer) printf("Ignoring spaces: \"%s\"\n",yytext);
^[[:blank:]]+\n+ if(debug_lexer) printf("Ignoring new line at the begin of a line\n");

 /* quoted text */
\"[[:alnum:][:blank:]%]*\" yylval->text=strndup(yytext+1,strlen(yytext)-2);if(debug_lexer) printf("found string \"%s\", length=%d, copied: \"%s\"\n",yytext,(int)strlen(yytext),yylval->text); return QUOTED_TEXT;

 /* incrementing icorr */
\n* yyextra->curr_corr_named=0; yyextra->ncorr++;if(debug_lexer) printf("Incrementing icorr: %d\n",yyextra->ncorr); return ENDL;

 /* operators */
\+ CORR_NAME_ADD("+"); return '+';
-  CORR_NAME_ADD("-"); return '-';
\( CORR_NAME_ADD("("); return '(';
\) CORR_NAME_ADD(")"); return ')';
\* CORR_NAME_ADD("*"); return '*';
\/ CORR_NAME_ADD("/"); return '/';

 /* equal */
\= return '=';

 /* real or imaginary */
Re if(debug_lexer) printf("Found Re\n"); CORR_NAME_ADD("Re"); return REAL_CORR;
Im if(debug_lexer) printf("Found Im\n"); CORR_NAME_ADD("Im"); return IMAG_CORR;

 /* recognizing interpolators */
(S|S0)  if(debug_lexer) printf("Found S0\n"); CORR_NAME_ADD("S0"); yylval->interpolator=0; return INTERPOLATOR;
(V1)    if(debug_lexer) printf("Found V1\n"); CORR_NAME_ADD("V1"); yylval->interpolator=1; return INTERPOLATOR;
(V2)    if(debug_lexer) printf("Found V2\n"); CORR_NAME_ADD("V2"); yylval->interpolator=2; return INTERPOLATOR;
(V3)    if(debug_lexer) printf("Found V3\n"); CORR_NAME_ADD("V3"); yylval->interpolator=3; return INTERPOLATOR;
(V4|V0) if(debug_lexer) printf("Found V4\n"); CORR_NAME_ADD("V0"); yylval->interpolator=4; return INTERPOLATOR;
(P|P5)  if(debug_lexer) printf("Found P5\n"); CORR_NAME_ADD("P5"); yylval->interpolator=5; return INTERPOLATOR;
(A1)    if(debug_lexer) printf("Found A1\n"); CORR_NAME_ADD("A1"); yylval->interpolator=6; return INTERPOLATOR;
(A2)    if(debug_lexer) printf("Found A2\n"); CORR_NAME_ADD("A2"); yylval->interpolator=7; return INTERPOLATOR;
(A3)    if(debug_lexer) printf("Found A3\n"); CORR_NAME_ADD("A3"); yylval->interpolator=8; return INTERPOLATOR;
(A4|A0) if(debug_lexer) printf("Found A4\n"); CORR_NAME_ADD("A0"); yylval->interpolator=9; return INTERPOLATOR;
(T1)    if(debug_lexer) printf("Found T1\n"); CORR_NAME_ADD("T1"); yylval->interpolator=10;return INTERPOLATOR;
(T2)    if(debug_lexer) printf("Found T2\n"); CORR_NAME_ADD("T2"); yylval->interpolator=11;return INTERPOLATOR;
(T3)    if(debug_lexer) printf("Found T3\n"); CORR_NAME_ADD("T3"); yylval->interpolator=12;return INTERPOLATOR;
(B1)    if(debug_lexer) printf("Found B1\n"); CORR_NAME_ADD("B1"); yylval->interpolator=13;return INTERPOLATOR;
(B2)    if(debug_lexer) printf("Found B2\n"); CORR_NAME_ADD("B2"); yylval->interpolator=14;return INTERPOLATOR;
(B3)    if(debug_lexer) printf("Found B3\n"); CORR_NAME_ADD("B3"); yylval->interpolator=15;return INTERPOLATOR;
(C1)    if(debug_lexer) printf("Found C1\n"); CORR_NAME_ADD("C1"); yylval->interpolator=16;return INTERPOLATOR;
(C2)    if(debug_lexer) printf("Found C2\n"); CORR_NAME_ADD("C2"); yylval->interpolator=17;return INTERPOLATOR;
(C3)    if(debug_lexer) printf("Found C3\n"); CORR_NAME_ADD("C3"); yylval->interpolator=18;return INTERPOLATOR;

 /* recognizing numbers */
([0-9]*\.[0-9]+)|([0-9]+\.[0-9]*|([0-9]+)) if(debug_lexer) printf("Found DOUBLE: %s\n",yytext);sscanf(yytext,"%lg",&yylval->double_numb);CORR_NAME_ADD(yytext); return DOUBLE_NUMB;

%%

//initializator
two_pts_parser_driver::two_pts_parser_driver(const char *path)
{
  //open file                                                                                                            
  fin=fopen(path,"r");
  if(fin==NULL) crash("opening %s",path);

  //reset corr
  ncorr=0;

  init_scanner();
}

//initialize the scanner
void two_pts_parser_driver::init_scanner()
{
  curr_corr_named=0;
  yylex_init(&scanner);
  yyset_extra(this,scanner);
}

//destroy the scanner
void two_pts_parser_driver::destroy_scanner()
{
  yylex_destroy(scanner);
}

//read a list of two pts
two_pts_comp_t read_two_pts_sink_source_corr_from_file(const char *path)
{
  two_pts_parser_driver driver(path);
  two_pts_corr_parser_parse(&driver);
  
  //internal ncorr is wrong
  driver.output.corr_name=driver.corr_name;
  driver.output.ncorr=driver.ncorr;
  
  return driver.output;
}
